=LET(
blank,  ISBLANK([@Machine]),
noact,  ISBLANK([@Action]),
oops,   LEFT([@Action], 4) = "Miss",
ms,     LEFT([@Machine], 11) = "Multi-Slide",
lca,    IF(noact,"",LEFT([@Action],4)),
skip,   lca="Skip",
done,   lca="Done",
lies,   AND(ThisWeekMod < [@Wk], done),
late,   AND(done, [@Wk]>[@Due]),
diff,   ThisWeekMod - [@Due],
off,    IF(ms, 3, 5),
future, [@Due] > IF(ISBLANK([@Date]), ThisWeekMod, [@Wk])+1,
close,  [@Due]+1 > ThisWeekMod,
current,diff = 0,
past,   diff > 0,
miss,   AND(past, diff >= off),
next,   [@Due] = "XX",
IFS(
blank,  "",
oops,   "miss",
lies,   "lies",
next,   "next",
late,   "late",
skip,   "skip",
future, "earl",
done,   "done",
close,  "soon",
current,"this",
miss,   "miss",
past,   "past",
TRUE,    LNF
))